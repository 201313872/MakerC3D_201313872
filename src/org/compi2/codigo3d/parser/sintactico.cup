package org.compi2.codigo3d.parser;

import java_cup.runtime.Symbol;
import javax.swing.JOptionPane;

//PARSER PARA EL ARCHIVO DE ESCENARIOS
parser code
{://Codigo visible

    public void syntax_error(Symbol s){
        System.err.println(s);
    }

    public void unrecovered_syntax_error(Symbol s) throws java.lang.Exception{ 
        System.err.println("T.T");
        System.err.println(s);
    }

:}

action code
{://Codigo de acciones
      boolean logica =false;
:}

terminal String ID, ENTERO, DECIMAL;
terminal TokenC3D MAS, MENOS, MULT, DIV, POT, PAR_IZQ, PAR_DER, IGUAL, DIFERENTE, MAYOR, MENOR, MAYORI, MENORI, AND, OR, NOT;
nonterminal NodoC3D inicial, expresion_a, expresion_l, expresion_r;

precedence left MAS, MENOS;
precedence left MULT, DIV; 
precedence right POT; 
precedence left IGUAL, DIFERENTE, MAYOR, MENOR, MAYORI, MENORI;
precedence left NOT;
precedence left OR;
precedence left AND;


start with inicial;

inicial     ::= {: ControlC3D.reiniciar(); :} expresion_l:exp 
                    {: 
                       // JOptionPane.showMessageDialog(null,exp.getCad());
                        RESULT = exp;
                            if(logica){
                           // ControlC3D.setEtT(ControlC3D.getEtT()+" "+exp.getT());
                            ControlC3D.agregarC3D(ControlC3D.getEtT()+": V \n"+ ControlC3D.getEtF() +": F \n");
                            }else{
                        ControlC3D.agregarC3D("//Ãšltimo valor: " + exp.getCad());
                         }
                        :};

expresion_l     ::= expresion_l:izq  AND  expresion_l:der
                    {: 
                        String c3d = izq.getCad() +" \n" +izq.getT() + ": \n" + der.getCad() + "\n";
                        ControlC3D.agregarC3D(c3d);
                        NodoC3D NT = new NodoC3D("");
                        if(!ControlC3D.getEtF().contains(izq.getF())){
                        ControlC3D.setEtF(ControlC3D.getEtF()+" "+izq.getF());
                        }
                        ControlC3D.setEtF(ControlC3D.getEtF()+" "+der.getF());
                        ControlC3D.setEtT(der.getT());
                        NT.setT(der.getT());
                        NT.setF(der.getF());
                        logica=true;
                        RESULT = NT;
                    :}
                | expresion_l:izq  OR  expresion_l:der
                    {: 
                        String c3d = izq.getCad() +" \n" +izq.getF() + ": \n" + der.getCad() + "\n";
                        ControlC3D.agregarC3D(c3d);
                        NodoC3D NT = new NodoC3D("");
                        if(!ControlC3D.getEtT().contains(izq.getT())){
                        ControlC3D.setEtT(ControlC3D.getEtT()+" "+izq.getT());
                        }
                        ControlC3D.setEtT(ControlC3D.getEtT()+" "+der.getT());
                        ControlC3D.setEtF(der.getF());
                        NT.setT(der.getT());
                        NT.setF(der.getF());
                        
                        logica=true;
                        RESULT = NT;
                    :}
                | NOT  expresion_l:exp   
                    {: 
                        NodoC3D NT = new NodoC3D(exp.getCad()); 
                        if(!logica){
                            ControlC3D.agregarC3D(exp.getCad()+"\n");
                        ControlC3D.setEtF(exp.getT());
                        ControlC3D.setEtT(exp.getF());
                        }else{
                       
                        String temp1 = exp.getT();
                        NT.setT(exp.getF());
                        NT.setF(temp1);
                        String temp2 = ControlC3D.getEtF();
                        ControlC3D.setEtF(ControlC3D.getEtT());
                        ControlC3D.setEtT(temp2);
                        logica=true;}
                        RESULT = NT; 
                    :}
                |   PAR_IZQ expresion_l:exp PAR_DER   
                    {: RESULT = exp; :}
                | expresion_r:exp_r
                     {:
                     RESULT = exp_r; 
                     :};

expresion_r   ::= expresion_r:izq  IGUAL  expresion_r:der
                    {: 
                        String Tlab1 = ControlC3D.generaLab();
                        String Tlab2 = ControlC3D.generaLab();
                        String c3d = "if (" + izq.getCad() + "==" + der.getCad() +") go to "+Tlab1+ "\n go to "+ Tlab2  ;
                        //ControlC3D.agregarC3D(c3d);
                       NodoC3D NT = new NodoC3D(c3d);
                        NT.setT(Tlab1);
                        NT.setF(Tlab2);
                        RESULT = NT;
                    :}
                | expresion_r:izq  DIFERENTE  expresion_r:der
                    {: 
                        String Tlab1 = ControlC3D.generaLab();
                        String Tlab2 = ControlC3D.generaLab();
                        String c3d = "if (" + izq.getCad() + "!=" + der.getCad() +") go to "+Tlab1+ "\n go to "+ Tlab2  ;
                        //ControlC3D.agregarC3D(c3d);
                        NodoC3D NT = new NodoC3D(c3d);
                        NT.setT(Tlab1);
                        NT.setF(Tlab2);
                        RESULT = NT;
                    :}
                | expresion_r:izq  MAYOR  expresion_r:der
                    {: 
                        String Tlab1 = ControlC3D.generaLab();
                        String Tlab2 = ControlC3D.generaLab();
                        String c3d = "if (" + izq.getCad() + ">" + der.getCad() +") go to "+Tlab1+ "\n go to "+ Tlab2  ;
                        //ControlC3D.agregarC3D(c3d);
                        NodoC3D NT = new NodoC3D(c3d);
                        NT.setT(Tlab1);
                        NT.setF(Tlab2);
                        RESULT = NT;
                    :}
                | expresion_r:izq  MENOR  expresion_r:der
                    {: 
                        String Tlab1 = ControlC3D.generaLab();
                        String Tlab2 = ControlC3D.generaLab();
                        String c3d = "if (" + izq.getCad() + "<" + der.getCad() +") go to "+Tlab1+ "\n go to "+ Tlab2  ;
                        //ControlC3D.agregarC3D(c3d);
                        NodoC3D NT = new NodoC3D(c3d);
                        NT.setT(Tlab1);
                        NT.setF(Tlab2);
                        RESULT = NT;
                    :}
                | expresion_r:izq  MAYORI  expresion_r:der
                    {: 
                        String Tlab1 = ControlC3D.generaLab();
                        String Tlab2 = ControlC3D.generaLab();
                        String c3d = "if (" + izq.getCad() + ">=" + der.getCad() +") go to "+Tlab1+ "\n go to "+ Tlab2  ;
                        //ControlC3D.agregarC3D(c3d);
                        NodoC3D NT = new NodoC3D(c3d);
                        NT.setT(Tlab1);
                        NT.setF(Tlab2);
                        RESULT = NT;
                    :}
                | expresion_r:izq  MENORI  expresion_r:der
                    {: 
                        String Tlab1 = ControlC3D.generaLab();
                        String Tlab2 = ControlC3D.generaLab();
                        String c3d = "if (" + izq.getCad() + "<=" + der.getCad() +") go to "+Tlab1+ "\n go to "+ Tlab2  ;
                        //ControlC3D.agregarC3D(c3d);
                        NodoC3D NT = new NodoC3D(c3d);
                        NT.setT(Tlab1);
                        NT.setF(Tlab2);
                        RESULT = NT;
                    :}
                | expresion_a:exp_a 
                    {:
                    RESULT = exp_a;
                    :};


expresion_a   ::= expresion_a:izq MAS expresion_a:der
                    {: 
                        String temp = ControlC3D.generaTemp();
                        String c3d = temp + " = " + izq.getCad() + " + " + der.getCad() + ";\n";
                        ControlC3D.agregarC3D(c3d);
                        RESULT = new NodoC3D(temp); 
                    :}
            |   expresion_a:izq MENOS expresion_a:der
                    {: 
                        String temp = ControlC3D.generaTemp();
                        String c3d = temp + " = " + izq.getCad() + " - " + der.getCad() + ";\n";
                        ControlC3D.agregarC3D(c3d);
                        RESULT = new NodoC3D(temp); 
                    :}
            |   expresion_a:izq MULT expresion_a:der
                    {: 
                        String temp = ControlC3D.generaTemp();
                        String c3d = temp + " = " + izq.getCad() + " * " + der.getCad() + ";\n";
                        ControlC3D.agregarC3D(c3d);
                        RESULT = new NodoC3D(temp); 
                    :}
            |   expresion_a:izq DIV expresion_a:der
                    {: 
                        String temp = ControlC3D.generaTemp();
                        String c3d = temp + " = " + izq.getCad() + " / " + der.getCad() + ";\n";
                        ControlC3D.agregarC3D(c3d);
                        RESULT = new NodoC3D(temp); 
                    :}
            |   expresion_a:izq POT expresion_a:der
                    {: 
                        String temp = ControlC3D.generaTemp();
                        String c3d = temp + " = " + izq.getCad() + " ^ " + der.getCad() + ";\n";
                        ControlC3D.agregarC3D(c3d);
                        RESULT = new NodoC3D(temp); 
                    :}
            |   MENOS expresion_a:exp 
                    {: 
                        String temp = ControlC3D.generaTemp();
                        String c3d = temp + " = - " + exp.getCad() + ";\n";
                        ControlC3D.agregarC3D(c3d);
                        RESULT = new NodoC3D(temp); 
                    :}
            |   ID:id           
                    {: RESULT = new NodoC3D(id); :}
            |   ENTERO:entero   
                    {: RESULT = new NodoC3D(entero); :}
            |   DECIMAL:decimal 
                    {: RESULT = new NodoC3D(decimal); :}
            ;